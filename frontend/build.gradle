buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:0.5'
    }
}

apply plugin: 'base'
apply plugin: 'node'
apply plugin: 'idea'

idea {
    module {
        sourceDirs += file('app')
        testSourceDirs += file('test')
    }
}

repositories {
    jcenter()
}

task gulpBuild(type: NodeTask, dependsOn: 'npmInstall') {
    inputs.dir 'src'
    inputs.files 'gulpfile.js', 'bower.json', 'package.json'
    outputs.dir 'build/gulp/optimized'

    script = file('node_modules/gulp/bin/gulp.js')
    args = ['build-production']
}

task gulpServe(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ['default']
}

task gulpTest(type: NodeTask, dependsOn: 'npmInstall') {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ['test']
}

task deleteNodeModules(type: Delete) {
    delete 'node_modules'
}

clean.dependsOn deleteNodeModules

task test {
    dependsOn gulpTest
}

task build {
    dependsOn gulpBuild
}

task serve {
    dependsOn gulpServe
}

node {
    version = '0.10.26'
    download = true
}
