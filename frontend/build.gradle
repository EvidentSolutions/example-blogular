plugins {
    id "com.moowork.node" version "0.9"
}

apply plugin: 'base'
apply plugin: 'idea'

idea {
    module {
        sourceDirs += file('app')
        testSourceDirs += file('test')
    }
}

node {
    version = '0.12.3'
    download = true
}

task bowerInstall(type: NodeTask, dependsOn: npmInstall) {
    inputs.files '.bowerrc', 'bower.json'
    outputs.dir 'bower_components'

    script = file('node_modules/bower/bin/bower')
    args = ['install']
}

task installDependencies {
    dependsOn npmInstall
    dependsOn bowerInstall
}

task buildProduction(type: NodeTask, dependsOn: installDependencies) {
    inputs.dir 'app'
    inputs.files 'gulpfile.js', 'bower.json', 'package.json'
    outputs.dir 'build/egb/optimized'

    script = file('node_modules/gulp/bin/gulp.js')
    args = ['build:production']
}

task watch(type: NodeTask, dependsOn: installDependencies) {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ['watch']
}

task test(type: NodeTask, dependsOn: installDependencies) {
    script = file('node_modules/gulp/bin/gulp.js')
    args = ['test']
}

assemble.dependsOn buildProduction

task frontendJar(type: Jar, dependsOn: buildProduction) { t ->
    baseName "blogular-frontend"
    from "build/egb/optimized"
    eachFile { details ->
        details.path = "blogular-frontend/${details.path}"
    }
    // Jar contains duplicate empty folders, see Gradle issue:
    // http://issues.gradle.org/browse/GRADLE-1830
    // so we need to set includeEmptyDirs to false
    includeEmptyDirs = false
}
