plugins {
    id "com.bmuschko.tomcat" version "2.2.2"
}

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

ext {
    springVersion = '4.1.6.RELEASE'
    springSecurityVersion = '4.0.1.RELEASE'
    tomcatVersion = '7.0.53'
}

dependencies {
    compile "org.springframework:spring-webmvc:$springVersion"
    compile "org.springframework:spring-tx:$springVersion"
    compile "org.springframework:spring-jdbc:$springVersion"
    compile "org.springframework:spring-context-support:$springVersion"
    compile "org.springframework:spring-websocket:$springVersion"
    compile "org.springframework:spring-messaging:$springVersion"

    compile "org.springframework.security:spring-security-web:$springSecurityVersion"
    compile "org.springframework.security:spring-security-config:$springSecurityVersion"

    compile "com.fasterxml.jackson.core:jackson-databind:2.3.3"
    compile "fi.evident.dalesbred:dalesbred:0.6.0"
    compile "org.flywaydb:flyway-core:3.0"
    compile "org.jetbrains:annotations:13.0"

    compile "org.slf4j:slf4j-api:1.7.7"
    runtime "org.slf4j:jcl-over-slf4j:1.7.7"
    runtime "ch.qos.logback:logback-classic:1.1.2"

    runtime "org.hsqldb:hsqldb:2.3.2"

    providedCompile "javax.servlet:javax.servlet-api:3.0.1"

    testCompile "junit:junit:4.11"
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "com.jayway.jsonpath:json-path:0.9.1"

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion"
    tomcat "org.apache.tomcat.embed:tomcat-embed-logging-juli:$tomcatVersion"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }

    configurations {
        compile.exclude module: 'commons-logging'
    }
}

task bundledWar(type: War) {
    baseName "blogular-bundled"
    classpath tasks.findByPath(':frontend:frontendJar')
}

task runStandalone(type: com.bmuschko.gradle.tomcat.tasks.TomcatRunWar, dependsOn: bundledWar) {
    webApp = bundledWar.outputs.files.singleFile
    contextPath = '/'
    configFile = file("$projectDir/etc/standalone-context.xml")
}
